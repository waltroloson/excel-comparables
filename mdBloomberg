Option Explicit
Option Compare Text

Public Sub GetDataBloomberg(lRow As Long, sSymbol As String)
  On Error GoTo Err_Handler
  Dim i As Long
  Dim sURL As String, sHTML As String
  Dim stmp As String, sVal As String
  Dim oHttp As Object
  Dim dPrice As Double, dEarnings As Double
  Dim dPE As Double
  
  sURL = Replace(g_BloombergURL, "#symb#", sSymbol)

  Set oHttp = CreateObject("MSXML2.ServerXMLHTTP.6.0")
  oHttp.Open "GET", sURL, False
  oHttp.Send
  sHTML = oHttp.responseText
  
  sHTML = Replace(sHTML, """", "")
  
  'Price
  i = InStr(sHTML, "itemprop=price")
  If i > 0 Then dPrice = GetNumberYahoo(AdvancedTrim(GetTextBetween(sHTML, "content=", "/", i + 1)))
  
  'Eranings per share
  i = InStr(sHTML, "earningsPerShare")
  If i > 0 Then dEarnings = GetNumberYahoo(AdvancedTrim(GetTextBetween(sHTML, ":", ",", i + 1)))
  
  'Price Earnings Ratio
  i = InStr(sHTML, "priceEarningsRatio")
  If i > 0 Then dPE = GetNumberYahoo(AdvancedTrim(GetTextBetween(sHTML, ":", ",", i + 1)))
  
  'P/E
  If dPE <> 0 Then
    shData.Cells(lRow, Range("rBloomberg_PE").Column).Value = dPE
  ElseIf dEarnings = 0 Then
    shData.Cells(lRow, Range("rBloomberg_PE").Column).Value = ""
  Else
    shData.Cells(lRow, Range("rBloomberg_PE").Column).Value = dPrice / dEarnings
  End If
  
  'P/S
  i = InStr(sHTML, "priceToSalesRatio")
  If i > 0 Then
    shData.Cells(lRow, Range("rBloomberg_PS").Column).Value = GetNumberYahoo(AdvancedTrim(GetTextBetween(sHTML, ":", ",", i + 1)))
  Else
    shData.Cells(lRow, Range("rBloomberg_PS").Column).Value = ""
  End If
  
  'EPS
  If dEarnings <> 0 Then
    shData.Cells(lRow, Range("rBloomberg_EPS").Column).Value = dEarnings
  Else
    shData.Cells(lRow, Range("rBloomberg_EPS").Column).Value = ""
  End If
  
  'Market Cap
  i = InStr(sHTML, "marketCap")
  If i > 0 Then
    shData.Cells(lRow, Range("rBloomberg_MCap").Column).Value = GetNumberYahoo(AdvancedTrim(GetTextBetween(sHTML, ":", ",", i + 1)))
  Else
    shData.Cells(lRow, Range("rBloomberg_MCap").Column).Value = ""
  End If
  
  'Shares Outstanding
  i = InStr(sHTML, "sharesOutstanding")
  If i > 0 Then
    shData.Cells(lRow, Range("rBloomberg_Shares").Column).Value = GetNumberYahoo(AdvancedTrim(GetTextBetween(sHTML, ":", ",", i + 1)))
  Else
    shData.Cells(lRow, Range("rBloomberg_Shares").Column).Value = ""
  End If
  
  'Dividend Ind. Gross Yield
  i = InStr(sHTML, "indicatedGrossDividendYield")
  If i > 0 Then
    shData.Cells(lRow, Range("rBloomberg_DIGY").Column).Value = GetNumberYahoo(AdvancedTrim(GetTextBetween(sHTML, ":", ",", i + 1))) / 100
  Else
    shData.Cells(lRow, Range("rBloomberg_DIGY").Column).Value = ""
  End If
  
  'Currency
  i = InStr(sHTML, "issuedCurrency")
  If i > 0 Then
    shData.Cells(lRow, Range("rCurrBloomberg").Column).Value = AdvancedTrim(GetTextBetween(sHTML, ":", ",", i + 1))
  Else
    shData.Cells(lRow, Range("rCurrBloomberg").Column).Value = ""
  End If
  
Exit_Label:
  On Error Resume Next
  Set oHttp = Nothing
  Exit Sub
Err_Handler:
  MsgBox Err.Description, vbCritical, "GetDataBloomberg"
  Resume Exit_Label
End Sub
